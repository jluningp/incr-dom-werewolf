open Core_kernel
open Async_kernel

module type S = sig
  module Query : sig
    type t [@@deriving sexp]
  end

  module Response : sig
    type t [@@deriving sexp]
  end

  val name : string
end

module Conn = struct
  module Client = struct
    type t = { path : string }

    let create ~path = { path }
  end

  module Server = struct
    type t = (Sexp.t -> Sexp.t) String.Table.t

    let create () = String.Table.create ()

    let get_response (t : t) uri =
      let%bind.Option key = Uri.get_query_param uri "key" in
      let%bind.Option f = Hashtbl.find t key in
      let%bind.Option query = Uri.get_query_param uri "data" in
      Option.try_with (fun () ->
          let query = Sexp.of_string query in
          Sexp.to_string (f query))
  end
end

module Make (Rpc : S) = struct
  open Rpc

  let send_get_request_js uri =
    let open Js_of_ocaml in
    let xml = XmlHttpRequest.create () in
    let xml_response = Ivar.create () in
    let () =
      xml ## (_open (Js.string "GET") (Js.string (Uri.to_string uri)) Js._true)
    in
    xml##.onreadystatechange :=
      Js.wrap_callback (fun _ ->
          match xml##.readyState with
          | XmlHttpRequest.DONE ->
              let response_text =
                Js.Opt.case xml##.responseText
                  (fun () -> Js.string "")
                  (fun s -> s)
              in
              Ivar.fill_if_empty xml_response response_text
          | _ -> ());
    xml ## (send Js.null);
    let%map response_js = Ivar.read xml_response in
    Js.to_string response_js

  let dispatch (conn : Conn.Client.t) (query : Query.t) =
    let uri =
      Uri.make ~path:conn.path
        ~query:
          [
            ("key", [ Rpc.name ]);
            ("data", [ Sexp.to_string (Query.sexp_of_t query) ]);
          ]
        ()
    in
    let%map response = send_get_request_js uri in
    Or_error.try_with (fun () ->
        response |> Sexp.of_string |> Rpc.Response.t_of_sexp)

  let register_implementation (conn : Conn.Server.t)
      ~(f : Query.t -> Response.t) =
    Hashtbl.add_exn conn ~key:Rpc.name ~data:(fun sexp ->
        let query = Query.t_of_sexp sexp in
        let response = f query in
        Response.sexp_of_t response)
end
